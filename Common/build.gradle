plugins {
    id 'java'
    id 'org.spongepowered.gradle.vanilla' version '0.2.1-SNAPSHOT'
    id 'maven-publish'
}

archivesBaseName = "${modName}-common-${minecraftVersion}"

minecraft {
    version(minecraftVersion)
    runs {   
        if (project.hasProperty('commonRunsEnabled') ? project.findProperty('commonRunsEnabled').toBoolean() : true) {
        
            server(project.hasProperty('commonServerRunName') ? project.findProperty('commonServerRunName') : 'vanilla_server') {
                workingDirectory(this.file("run"))
            }
            client(project.hasProperty('commonClientRunName') ? project.findProperty('commonClientRunName') : 'vanilla_client') {
                workingDirectory(this.file("run"))
            }
        }
    }
}

dependencies {
     compileOnly group:'org.spongepowered', name:'mixin', version:'0.8.5'
     implementation group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.1'
}

processResources {

    def buildProps = project.properties.clone()

    filesMatching(['pack.mcmeta']) {

        expand buildProps
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId project.archivesBaseName
            version project.version
            from components.java
        }
    }

    repositories {
        maven {
            url "file://" + System.getenv("local_maven")
        }
    }
}